plugins {
    id 'java'
}
apply plugin: 'maven'
apply plugin: 'signing'
group 'net.mahmutkocas'
version '0.3'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'

                name 'osm-parser'
                description 'Open Street Map XML Parser written in Java'
                url 'https://github.com/TheDoctorOne/osm-parser'

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                scm {
                    connection 'scm:git:https://github.com/TheDoctorOne/osm-parser'
                    developerConnection 'scm:git:https://github.com/TheDoctorOne/osm-parser'
                    url 'https://github.com/TheDoctorOne/osm-parser'
                }

                developers {
                    developer {
                        id 'mahmut.kocas'
                        name 'Mahmut Kocas'
                        email 'mahmudkcoas@gmail.com'
                    }
                }
            }
        }
    }
}
